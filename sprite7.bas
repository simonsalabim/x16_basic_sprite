100 REM **LETS DO SPRITES, LESSON 7, PICK UP SPRITE WITH THE MOUSE
110 REM
120 REM **** SETUP SCREEN ****
130 SCREEN $80 : REM 320x200@256C
140 X = 152 : Y = 92
150 REM
160 REM **WE WILL PICK UP THE SPRITE AND DROP IT IN A SPECIFIC
170 REM **SPOT ON THE SCREEN
180 REM
190 REM **** SPRITE DATA ****
200 DATA 0,0,0,0,0,6,6,6,6,6,6,0,0,0,0,0
210 DATA 0,0,0,6,6,6,6,6,6,6,6,6,6,0,0,0
220 DATA 0,0,6,6,6,6,6,6,6,6,6,6,6,6,0,0
230 DATA 0,6,6,6,6,6,6,6,6,6,6,6,6,6,6,0
240 DATA 0,6,6,6,6,6,6,6,6,6,6,6,6,6,6,0
250 DATA 6,6,6,6,6,6,6,6,6,6,6,6,6,6,6,6
260 DATA 6,6,6,6,6,6,6,7,7,6,6,6,6,6,6,6
270 DATA 6,6,6,6,6,6,7,7,7,7,6,6,6,6,6,6
280 DATA 6,6,6,6,6,6,7,7,7,7,6,6,6,6,6,6
290 DATA 6,6,6,6,6,6,6,7,7,6,6,6,6,6,6,6
300 DATA 6,6,6,6,6,6,6,6,6,6,6,6,6,6,6,6
310 DATA 0,6,6,6,6,6,6,6,6,6,6,6,6,6,6,0
320 DATA 0,6,6,6,6,6,6,6,6,6,6,6,6,6,6,0
330 DATA 0,0,6,6,6,6,6,6,6,6,6,6,6,6,0,0
340 DATA 0,0,0,6,6,6,6,6,6,6,6,6,6,0,0,0
350 DATA 0,0,0,0,0,6,6,6,6,6,6,0,0,0,0,0
360 REM
370 REM **** LOAD SPRITE DATA ****
380 FOR J = 0 TO 255
390 READ D
400 VPOKE $0,$C000+J,D
410 NEXT J
420 REM
430 REM **LETS SET THE SPRITES ATTRIBUTES NOW
440 REM
450 REM **** SET SPRITE ATTRIBUTES ****
460 VPOKE $1, $FC08, $00 : REM ADDRESS
470 VPOKE $1, $FC09, $86 : REM MODE, ADDRESS
480 VPOKE $1, $FC0A, X : REM X POSITION
490 VPOKE $1, $FC0B, $00 : REM X POSITION
500 VPOKE $1, $FC0C, Y : REM Y POSITION
510 VPOKE $1, $FC0D, $00 : REM Y POSITION
520 VPOKE $1, $FC0E, $0C : REM COLLISION MASK, ZDEPTH, VFLIP, HFLIP
530 VPOKE $1, $FC0F, $50 : REM HEIGHT, WIDTH, PALETTE OFFSET
540 REM
550 REM **NOW LETS ENABLE SPRITES
560 REM
570 REM **** ENABLE SPRITES ****
580 POKE $9F29, PEEK($9F29) OR $40
590 REM
600 REM **TURN ON THE MOUSE
610 REM
612 MOUSE 1
630 REM **WHEN WE CLICK CHECK IF WE ARE ON THE SPRITE
640 REM **IF WE ARE MOVE THE SPRITE WITH THE MOUSE UNTIL THE USER
650 REM **LETS GO OF THE BUTTON
660 REM **IF THE SPRITE IS OVER THE BOX THEN PLACE THE SPRITE OTHERWISE
670 REM **PUT THE SPRITE BACK WHERE IT WAS
675 CARRY = 0
680 RECT 34,34,66,66,5
690 IF CA=0 AND MB=1 THEN GOSUB 800 : REM PICK UP SPRITE
700 IF CA=1 AND MB<>1 THEN GOSUB 900 : REM DROP SPRITE
710 IF CA=1 AND MB=1 THEN GOSUB 1000 : REM UPDATE SPRITE POSITION
720 GOTO 690
800 IF MX>=X-8 AND MX<=X+8 AND MY<=Y+8 AND MY>=Y-8 THEN CA = 1
810 RETURN
900 CA=0
905 IF MX>=34 AND MX<=66 AND MY>=34 AND MY<=66 GOTO 950 : REM MOUSE IN BOX
910 VPOKE $1, $FC0A, X AND $FF
915 VPOKE $1, $FC0B, INT(X/256)
920 VPOKE $1, $FC0C, Y
930 GOTO 990
950 VPOKE $1, $FC0A, 50
960 VPOKE $1, $FC0C, 50
990 RETURN
1000 VPOKE $1, $FC0A, MX AND $FF
1005 VPOKE $1, $FC0B, INT(MX/256)
1010 VPOKE $1, $FC0C, MY
1020 RETURN
