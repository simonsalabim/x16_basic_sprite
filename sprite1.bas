100 REM **LETS DO SPRITES, LESSON 1, SHOW A SPRITE
110 REM
120 REM **** SETUP SCREEN ****
130 SCREEN $80 : REM 320x200@256C
140 REM
150 REM **FIRST THING WE NEED IS DATA FOR THE SPRITE
160 REM **THE SMALLEST SPRITE IS 8x8
170 REM **WE WILL USE THE DEFAULT C64 COLOR PALETTE
180 REM **6 IS BLUE AND 7 IS YELLOW
190 REM
200 REM **** SPRITE DATA ****
210 DATA 6,6,6,6,6,6,6,6
220 DATA 6,6,6,6,6,6,6,6
230 DATA 6,6,7,7,7,7,6,6
240 DATA 6,6,7,7,7,7,6,6
250 DATA 6,6,7,7,7,7,6,6
260 DATA 6,6,7,7,7,7,6,6
270 DATA 6,6,6,6,6,6,6,6
280 DATA 6,6,6,6,6,6,6,6
290 REM
300 REM **NOW WE HAVE A NICE BLUE SQUARE WITH YELLOW INTERIOR AS OUR SPRITE DATA
310 REM **IN ORDER TO USE SPRITES WE NEED TO ENABLE THEM
320 REM **WE USE THE POKE COMMAND TO ALTER VERA REGISTERS
330 REM **ADDRESS $9F29 CONTAINS THE SPRITE ENABLE FLAG AT BIT 6
340 REM **01000000 IN HEX IS 40
350 REM
360 REM **** ENABLE SPRITES ****
370 POKE $9F29, PEEK($9F29) OR $40
380 REM
390 REM **THE DATA NEEDS TO GO INTO VIDEO RAM WHICH GOES FROM $00000-$1F9BF
400 REM **LETS CHOOSE $0C0000 AS WHERE THE DATA WILL GO
410 REM **WE LOOP THROUGH THE SPRITE DATA AND PUSH THE DATA INTO VIDEO RAM
420 REM **VIDEO RAM IS BANKED AND WE WILL USE THE FIRST BANK
430 REM
440 REM **** LOAD SPRITE DATA ****
450 FOR I = 0 TO 63
460 READ D
470 VPOKE $0,$C000+I,D
480 NEXT I
490 REM
500 REM **NOW THAT THE SPRITE IS LOADED WE DISPLAY IT USING SPRITE ATTRIBUTES
510 REM **THERE ARE 8 ATTRIBUTE BYTES FOR EACH SPRITE
520 REM **SPRITE ATTRIBUTES GO FROM $1FC00-$1FFFF GIVING US 128 SPRITES
530 REM **WE NEED TO DEFINE THE HEIGHT AND WIDTH
540 REM **THE ZDEPTH WHICH IS WHERE THE SPRITE IS LOCATED IN THE LAYERS
550 REM **THE SPRITE POSITION ON THE SCREEN
560 REM **THE SPRITE MODE (4BPP or 8BPP)
570 REM **AND FINALLY THE ADDRESS OF THE SPRITE DATA
580 REM
590 REM **** SET SPRITE ZERO ATTRIBUTES ****
600 VPOKE $1, $FC00, $00 : REM ADDRESS (12:5)
610 VPOKE $1, $FC01, $86 : REM MODE-3 BLANK BITS-ADDRESS (16:13)
620 VPOKE $1, $FC02, 160 : REM X POSITION (7:0)
630 VPOKE $1, $FC03, $00 : REM 6 BLANK BITS-X (9:8)
640 VPOKE $1, $FC04, 100 : REM Y POSITION (7:0)
650 VPOKE $1, $FC05, $00 : REM 6 BLANK BITS-Y (9:8)
660 VPOKE $1, $FC06, $0C : REM COLLISION MASK (3:0)-ZDEPTH (1:0)-VFLIP-HFLIP
670 VPOKE $1, $FC07, $00 : REM HEIGHT (1:0)-WIDTH (1:0)-PALETTE OFFSET (3:0)
680 REM
690 REM **SO WE SET THE ADDRESS TO C000 BY DROPPING 5 BITS GIVING US 600 IN
700 REM **HEX AND THEN DROPPING THE TWO END BYTES LEAVING US WITH 6
710 REM **THE 8 FOR BYTE 2 COMES FROM USING MODE 1 (1000 1100)
720 REM **WE PUT THE SPRITE IN THE MIDDLE OF THE SCREEN 160 100
730 REM **SPRITES ARE LOCATED USING THE UPPER LEFT CORNER
740 REM **WE SET THE ZDEPTH TO 11 (0000 1100)
750 REM **AND THEN THE HEIGHT AND WIDTH ARE SET TO 0 (00 00 8x8)
760 REM
770 REM **VOILA AND WE HAVE A BLUE SQUARE WITH YELLOW CENTER SPRITE