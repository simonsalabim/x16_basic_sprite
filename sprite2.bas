100 REM **LETS DO SPRITES, LESSON 2, MOVE A SPRITE
110 REM
120 REM **** SETUP SCREEN ****
130 SCREEN $80 : REM 320x200@256C
140 X = 160-16 : Y = 100-16 : REM WE CENTER THE 32x32 SPRITE
150 REM
160 REM **LETS SET UP A SPRITE AS IN LESSON 1 BUT BIGGER
170 REM **WE WILL MAKE THE SPRITE 32x32 THIS TIME AND GENERATE DATA
180 REM
190 REM **** LOAD SPRITE DATA ****
200 FOR I = 0 TO 31
210 FOR J = 1 TO 32
220 IF I<3 OR I>28 THEN VPOKE $0,$C000+(J +(32*I))-1,6 : GOTO 250
230 IF J<4 OR J>29 THEN VPOKE $0,$C000+(J +(32*I))-1,6 : GOTO 250
240 VPOKE $0,$C000+(J +(32*I))-1,7
250 NEXT J
260 NEXT I
270 REM
280 REM **NOW LETS SET UP SPRITES LIKE WE DO IN LESSON 1
290 REM
300 REM **** ENABLE SPRITES ****
310 POKE $9F29, PEEK($9F29) OR $40
320 REM
330 REM **NOW SET UP THE ATTRIBUTES BUT WE CHANGE THE HEIGHT AND WIDTH
340 REM
350 REM **** SET UP SPRITE ZERO ATTRIBUTES ****
360 GOSUB 1000
370 REM
380 REM **THIS TIME WE SET THE HEIGHT AND WIDTH TO 32 (1010)
390 REM
400 REM **WE MOVE THE SPRITE USING THE POSITION ATTRIBUTES
410 REM
420 REM **** MOVE SPRITE ****
430 GET A$ : IF A$="" THEN 430
440 A=ASC(A$)
450 IF A=68 THEN X=X+1 : IF X>288 THEN X=288 : REM D
460 IF A=65 THEN X=X-1 : IF X<0 THEN X=0 : REM A
470 IF A=87 THEN Y=Y-1 : IF Y<0 THEN Y=0 : REM W
480 IF A=88 THEN Y=Y+1 : IF Y>168 THEN Y=168 : REM X
490 IF A=81 THEN 600 : REM Q
500 VPOKE $1, $FC02, X AND $FF : REM X POSITION SPREAD OVER 2 BYTES
510 VPOKE $1, $FC03, INT(X/256) : REM SO WE NEED TO SPLIT THE VALUE
520 VPOKE $1, $FC04, Y : REM Y IS LESS THAN 256 SO NO NEED TO SPLIT
530 GOTO 430
540 REM
550 REM **THERE IS SOMETHING SPECIAL ABOUT SPRITE 0 (THE ONE WE ARE USING)
560 REM **IT IS THE SPRITE FOR THE MOUSE CURSOR
570 REM **IF WE TURN ON THE MOUSE AND RESET OUR SPRITE, BIG BOX MOUSE!
580 MOUSE 1
590 GOSUB 1000
600 END
1000 REM **** SPRITE 0 ATTRIBUTES SUBROUTINE ****
1010 VPOKE $1, $FC00, $00 : REM ADDRESS
1020 VPOKE $1, $FC01, $86 : REM MODE, ADDRESS
1030 VPOKE $1, $FC02, X : REM X POSITION
1040 VPOKE $1, $FC03, $00 : REM X POSITION
1050 VPOKE $1, $FC04, Y : REM Y POSITION
1060 VPOKE $1, $FC05, $00 : REM Y POSITION
1070 VPOKE $1, $FC06, $0C : REM COLLISION MASK, ZDEPTH, VFLIP, HFLIP
1080 VPOKE $1, $FC07, $A0 : REM HEIGHT, WIDTH, PALETTE OFFSET
1090 RETURN
